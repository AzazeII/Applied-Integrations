buildscript {
    repositories {
        jcenter()
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "org.sonarqube" version "2.7"
    id 'io.spring.dependency-management' version '1.0.1.RELEASE'
}

apply plugin: 'net.minecraftforge.gradle.forge'

def getGitBranch = { ->
    // Create std output stream, to read command line output
    def stdout = new ByteArrayOutputStream()

    // Execute next block:
    exec {
        // Get commit hash
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'

        // Write standart output
        standardOutput = stdout
    }

    // Return hash from stdout
    return stdout.toString().trim()
}

def getGitCommitHash = { ->
    // Create std output stream, to read command line output
    def stdout = new ByteArrayOutputStream()

    // Execute next block:
    exec {
        // Get commit hash
        commandLine 'git', 'rev-parse', '--short', 'HEAD'

        // Write standart output
        standardOutput = stdout
    }

    // Return hash from stdout
    return stdout.toString().trim()
}

version = getGitBranch() + '-' + getGitCommitHash()
group = "com.azazell" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AppliedIntegrations"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }

    // Include forge :) Or it will be not minecraft, but "forge not included"
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }

    // Include tesla api
    maven {
        url 'http://maven.mcmoddev.com'
    }

    // Include ic2 maven
    maven {
        name 'ic2'
        url 'http://maven.ic2.player.to'
    }

    // Maven to download botania
    maven {
        name = "botania"
        url = "https://maven.blamejared.com/"
    }

    maven { url "https://maven.thiakil.com/" }
}

dependencyManagement {
    imports {
        mavenBom 'org.apache.logging.log4j:log4j-bom:2.11.2'
    }
}

dependencies {
    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    compile "mezz.jei:jei_1.12.2:4.13.0.219"
    compile "appeng:appliedenergistics2:rv6-stable-6"
    compile "cofh:RedstoneFlux:1.12-2.1.0.6:universal"
    //compile 'baubles:Baubles:1.12:1.5.2'
    compile 'org.json:json:20171018'

    compile 'org.apache.logging.log4j:log4j-api'
    compile 'org.apache.logging.log4j:log4j-core'
    
    compile "com.sparkjava:spark-core:2.8.0"
    compile "net.darkhax.tesla:Tesla-1.12.2:1.0.63"
    compile "mekanism:Mekanism:1.12.2:9.4.13.349"

    compile "net.industrial-craft:industrialcraft-2:2.8.111-ex112:api"
    compile "vazkii.botania:Botania:r1.10-359.39"
    compile "teamroots.embers:EmbersRekindled:1.13"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}